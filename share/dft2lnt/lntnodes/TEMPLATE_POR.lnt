module TEMPLATE_POR(TEMPLATE_COMMON) is

	process POR [FAIL : NAT_CHANNEL, ACTIVATE : NAT_BOOL_CHANNEL] (total : NAT, received : BOOL_ARRAY) is
	var
		done : BOOL,
		failed : BOOL,
		failed_before : BOOL,
		nr : NAT,
		nr_act: NAT
	in
		done := FALSE;
		failed := FALSE;
		failed_before := FALSE;
		nr_act := 0 of NAT;
		loop
			select

			(*
			 * Listen for any child to be triggered.
			 * If the first child fails before all others, start the failure procedure of the gate
			 *)
			FAIL (?nr) where (1 < nr) and (nr <= total);
			if ((not(failed)) and (not(received[nr]))) then
				received[nr] := TRUE;
				failed_before := TRUE
			end if;
			nr := 0

			(*
			 * If the first child fails before all others, start the failure procedure of the gate
			 *)
			[]
				FAIL (?nr) where (nr == 1);
				if ((not(failed_before)) and (not(received[nr]))) then
					received[nr] := TRUE;
					failed := TRUE
				end if;
				nr := 0

			(*
			* If this node is activated, enable the activation of the source.
			*)
			[]
				ACTIVATE (?nr,FALSE) where (nr == (0 of NAT));
				if (nr_act == 0 of NAT) then
					nr_act := 1 of NAT
				end if

			(*
			* If the children should be activated, do so, one by one,
			* from left to right.
			*)
			[]
				if (nr_act > 0) then
					ACTIVATE (!nr_act,TRUE);
					if nr_act < total  then
						nr_act := nr_act + 1
					else
						nr_act := 0
					end if
				end if

			(*
			 * Failure Procedure for the POR gate
			 *)
			[]
				if ((failed) and (not(done))) then
					FAIL (!0 of NAT);
					done := TRUE
				end if
			end select
		end loop
	end var
	end process


end module
